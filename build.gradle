buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.2'
    }
}

apply plugin: "com.android.application"

//Shamelessly copied from philm
def versionMajor = 1 //Major change external and internal
def versionMinor = 0 //Major Internal Change
def versionPatch = 1 //Moderate internal change
def versionBuild = 3 //Quickfixes and random bugfixes

android {
    compileSdkVersion 21
    buildToolsVersion '21.0.2'

    defaultConfig {
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        minSdkVersion 9
        targetSdkVersion 21
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {
            storeFile file(projectDir.getAbsolutePath()+RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {

        debug{
            versionNameSuffix "-debug"
        }

        release {
            signingConfig signingConfigs.release
        }
    }

    sourceSets {
        main {
            manifest.srcFile "AndroidManifest.xml"
            java.srcDir "src"
            res.srcDir "res"
            assets.srcDir "assets"

        }
    }
}

repositories {
    mavenCentral()
    maven { url 'http://repo1.maven.org/maven2' }
}

dependencies {
    compile 'com.android.support:support-v4:20.0.0'
    compile 'com.android.support:appcompat-v7:20.0.0'
    compile project(":libs:html-textview")
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'com.github.castorflex.smoothprogressbar:library:0.5.1'
    compile 'se.emilsjolander:stickylistheaders:2.5.0'
    compile 'com.github.johnpersano:supertoasts:1.3.4@aar'
    compile 'org.apache.commons:commons-lang3:3.3.2'
    compile 'com.squareup.okhttp:okhttp:2.0.0'
    compile 'com.squareup.okio:okio:1.0.1'
    compile 'org.java-websocket:Java-WebSocket:1.3.0'
    compile 'com.squareup:otto:1.3.5'
    compile 'nl.qbusict:cupboard:2.0.1'
    compile 'com.github.gabrielemariotti.changeloglib:library:1.5.1'
}
